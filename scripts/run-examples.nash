#!/bin/nash

# Disgusting hack because you can't declare empty arrays at the moment
var failures = [""];
failures.pop();

for script in glob("./examples/*.nash") {

  var command = `nash "${script}"`;

  # This script requires user input...
  if script.ends_with("example-21.nash") {
    command = `echo test_input` => command;
  };

  out("Running script ${script}...");
  var (output, err) = exec command ? var code;
  out(output);

  if code != 0 {
    var code = code.fmt();
    out("Nash script exited with non-zero code (${code})");
    out(output);
    out(err);
    failures.push(script);
  } else {
    out("Nash script ${script} succeeded");
  };

  out("");
};

if failures.len() != 0 {
  out("Some scripts failed to execute!");
  out(failures.fmt());
  exit 1;
} else {
  out("All scripts run successfully!");
};
